;Copyright (C) 2010-2011 Tom Schoonjans  

;This program is free software: you can redistribute it and/or modify
;it under the terms of the GNU General Public License as published by
;the Free Software Foundation, either version 3 of the License, or
;(at your option) any later version.

;This program is distributed in the hope that it will be useful,
;but WITHOUT ANY WARRANTY; without even the implied warranty of
;MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
;GNU General Public License for more details.

;You should have received a copy of the GNU General Public License
;along with this program.  If not, see <http://www.gnu.org/licenses/>.
;



!define PRODUCT_NAME "xmimsim"
!define VERSION @VERSION@
!define PRODUCT_WEB_SITE "http://github.com/tschoonj/xmimsim"
!define PRODUCT_DIR_REGKEY "Software\${PRODUCT_NAME}"
!define PRODUCT_UNINST_KEY "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_NAME}"
!define PRODUCT_UNINST_ROOT_KEY "HKLM"
!define GTK_INSTALLER_EXE "gtk2-runtime-2.22.0-2010-10-21-ash.exe"
!define MY_HOME "C:\msys\1.0\home\schoon\"
!define MY_MINGW "C:\MinGW64\"

!include "MUI2.nsh"

Name ${PRODUCT_NAME}
OutFile ${PRODUCT_NAME}-${VERSION}-win32.exe
RequestExecutionLevel none

InstallDir "$PROGRAMFILES\xmimsim"
InstallDirRegKey HKLM "Software\${PRODUCT_NAME}" "InstallationDirectory"

ShowInstDetails show
ShowUnInstDetails show


SetCompressor /SOLID lzma

SetOverwrite ifnewer
CRCCheck on

!define MUI_ABORTWARNING

!define MUI_WELCOMEPAGE_TEXT "Thank you for downloading xmimsim. This wizard will assist you in installing the required components to run the program"
!define MUI_WELCOMEPAGE_TITLE_3LINES
!insertmacro MUI_PAGE_WELCOME
!define MUI_LICENSEPAGE_CHECKBOX
!insertmacro MUI_PAGE_LICENSE "${MY_HOME}github\xmimsim\nsis\License.rtf"

!insertmacro MUI_PAGE_DIRECTORY
!insertmacro MUI_PAGE_INSTFILES
!define MUI_FINISHPAGE_TEXT "Thank you for installing xmimsim. Remember: the latest version of xmimsim can be obtained from the development repository on github"
!define MUI_FINISHPAGE_LINK "xmimsim development link"
!define MUI_FINISHPAGE_LINK_LOCATION ${PRODUCT_WEB_SITE} 
!define MUI_FINISHPAGE_RUN "$INSTDIR\Bin\xmimsim-gui.exe"
!define MUI_FINISHPAGE_RUN_NOTCHECKED
;!define MUI_FINISHPAGE_SHOWREADME "$INSTDIR\README.txt"
!insertmacro MUI_PAGE_FINISH


!insertmacro MUI_UNPAGE_CONFIRM
!insertmacro MUI_UNPAGE_INSTFILES
!insertmacro MUI_UNPAGE_FINISH


!insertmacro MUI_LANGUAGE "English"


Section "-xmimsim core files" SecDummy
;install dependencies
;first GTK2-WIN
SetShellVarContext all
SetOutPath "$INSTDIR"
DetailPrint "Installing GTK2 runtime"
File "nsis\${GTK_INSTALLER_EXE}"
ExecWait '"${GTK_INSTALLER_EXE}" /sideeffects=no /dllpath=root /translations=no /compatdlls=no /S /D=$INSTDIR\GTK2'
Delete "$INSTDIR\${GTK_INSTALLER_EXE}"

;check http://gtk-win.sourceforge.net/gsmartcontrol.nsi for some inspiration


;next: all the dependencies: gsl, fgsl, hdf5, libxml2, libxslt, libgomp...
SetOutPath "$INSTDIR/Lib"
DetailPrint "Installing GNU Scientific Library (GSL)"
!insertmacro InstallLib REGDLL NOTSHARED NOREBOOT_PROTECTED "${MY_HOME}bin\libgsl-0.dll" "$INSTDIR\Lib" "$INSTDIR"
!insertmacro InstallLib REGDLL NOTSHARED NOREBOOT_PROTECTED "${MY_HOME}bin\libgslcblas-0.dll" "$INSTDIR\Lib" "$INSTDIR"

DetailPrint "Installing GNU Scientific Library Fortran bindings (FGSL)"
!insertmacro InstallLib REGDLL NOTSHARED NOREBOOT_PROTECTED "${MY_HOME}bin\libfgsl-0.dll" "$INSTDIR\Lib" "$INSTDIR"

DetailPrint "Installing HDF5 (with Fortran bindings)"
!insertmacro InstallLib REGDLL NOTSHARED NOREBOOT_PROTECTED "${MY_HOME}bin\libhdf5-6.dll" "$INSTDIR\Lib" "$INSTDIR"
!insertmacro InstallLib REGDLL NOTSHARED NOREBOOT_PROTECTED "${MY_HOME}bin\libhdf5_fortran-6.dll" "$INSTDIR\Lib" "$INSTDIR"

DetailPrint "Installing libxml2"
!insertmacro InstallLib REGDLL NOTSHARED NOREBOOT_PROTECTED "${MY_HOME}bin\libxml2-2.dll" "$INSTDIR\Lib" "$INSTDIR"

DetailPrint "Installing libxslt"
!insertmacro InstallLib REGDLL NOTSHARED NOREBOOT_PROTECTED "${MY_HOME}bin\libxslt-1.dll" "$INSTDIR\Lib" "$INSTDIR"

DetailPrint "Installing xraylib (with Fortran bindings)"
!insertmacro InstallLib REGDLL NOTSHARED NOREBOOT_PROTECTED "${MY_HOME}bin\libxrl-3.dll" "$INSTDIR\Lib" "$INSTDIR"
!insertmacro InstallLib REGDLL NOTSHARED NOREBOOT_PROTECTED "${MY_HOME}bin\libxrlf03-3.dll" "$INSTDIR\Lib" "$INSTDIR"

DetailPrint "Installing gfortran runtime libraries"
!insertmacro InstallLib REGDLL NOTSHARED NOREBOOT_PROTECTED "${MY_MINGW}bin\libgfortran-3.dll" "$INSTDIR\Lib" "$INSTDIR"

DetailPrint "Installing OpenMP runtime libraries"
!insertmacro InstallLib REGDLL NOTSHARED NOREBOOT_PROTECTED "${MY_MINGW}bin\libgomp-1.dll" "$INSTDIR\Lib" "$INSTDIR"

DetailPrint "Installing POSIX threads runtime libraries"
!insertmacro InstallLib REGDLL NOTSHARED NOREBOOT_PROTECTED "${MY_MINGW}bin\pthreadGC2.dll" "$INSTDIR\Lib" "$INSTDIR"


;my own stuff
DetailPrint "Installing XMIMSIM runtime library"
!insertmacro InstallLib REGDLL NOTSHARED NOREBOOT_PROTECTED "${MY_HOME}bin\libxmimsim-0.dll" "$INSTDIR\Lib" "$INSTDIR"

DetailPrint "Installing XMIMSIM executables"
SetOutPath "$INSTDIR\Bin"
File "${MY_HOME}bin\xmimsim.exe"
File "${MY_HOME}bin\xmimsim-db.exe"
File "${MY_HOME}bin\xmimsim-gui.exe"
File "${MY_HOME}bin\xmimsim-pymca.exe"
File "${MY_HOME}bin\xmso2xmsi.exe"

SetOutPath "$INSTDIR\Share"
DetailPrint "Installing XMIMSIM HDF5 database"
File "${MY_HOME}github\xmimsim\build\bin\xmimsimdata.h5"

DetailPrint "Installing XMIMSIM XML and XSLT files"
File "${MY_HOME}github\xmimsim\xml\xmimsim-1.0.dtd"
File "${MY_HOME}github\xmimsim\xml\xmso2xmsi.xml"

;catalog needs to be produced upon installation
FileOpen $9 "$INSTDIR\Share\xmimsim_cat.xml"
FileWrite $9 "<?xml version=$\"1.0$\"?>$\r$\n"
FileWrite $9 "<!DOCTYPE catalog PUBLIC $\"-//OASIS//DTD Entity Resolution XML Catalog V1.0//EN$\" $\"http://www.oasis-open.org/committees/entity/release/1.0/catalog.dtd$\">$\r$\n"
FileWrite $9 "<catalog xmlns=$\"urn:oasis:names:tc:entity:xmlns:xml:catalog$\">$\r$\n"
FileWrite $9 "<rewriteURI uriStartString=$\"http://www.xmi.UGent.be/xml/$\" "
;make system call to produce URI
StrCpy $0 "$INSTDIR\Share\"
System::Call "shlwapi::UrlCreateFromPath(t r0, t .r1, *i ${NSIS_MAX_STRLEN} r2, i0)"
FileWrite $9 "rewritePrefix=$\"$1$\"/>$\r$\n"
FileWrite $9 "</catalog>$\r$\n"
FileClose $9
;this needs to be verified carefully!!!

;copy empty solid angles file to common appdata xmimsim folder
SetOutPath "$APPDATA\xmimsim"
File "${MY_HOME}github\xmimsim\build\bin\xmimsim-solid-angles.h5"

;path modification
${EnvVarUpdate} $0 "PATH" "A" "HKLM" "$INSTDIR\Bin"

;registry modifications
WriteRegStr HKLM "Software\${PRODUCT_NAME}" "InstallationDirectory" "$INSTDIR"
WriteRegStr HKLM "Software\${PRODUCT_NAME}" "Vendor" "Tom Schoonjans"
WriteRegStr HKLM "Software\${PRODUCT_NAME}\data" "" "$INSTDIR\Share\xmimsimdata.h5"
WriteRegStr HKLM "Software\${PRODUCT_NAME}\catalog" "" "$INSTDIR\Share\xmimsim_cat.xml"
WriteRegStr HKLM ${PRODUCT_UNINST_KEY} "DisplayName" "${PRODUCT_NAME} ${VERSION}"
WriteRegStr HKLM ${PRODUCT_UNINST_KEY} "DisplayVersion" "${VERSION}"
WriteRegDWORD HKLM ${PRODUCT_UNINST_KEY} "NoModify" 1
WriteRegDWORD HKLM ${PRODUCT_UNINST_KEY} "NoRepair" 1
WriteRegStr HKLM ${PRODUCT_UNINST_KEY} "UninstallString" "$\"$INSTDIR\Uninstall.exe$\""
WriteRegStr HKLM ${PRODUCT_UNINST_KEY} "QuietUninstallString" "$\"$INSTDIR\Uninstall.exe$\" /S"
WriteUninstaller "$INSTDIR\Uninstall.exe"
SectionEnd

Section "Uninstall"
SetShellVarContext all
SetAutoClose false

;GTK2
DetailPrint "Uninstalling GTK2 runtime"
ExecWait "$INSTDIR\GTK2\gtk2_runtime_uninst.exe /remove_config=yes /sideeffects=no /dllpath=root /translations=no /compatdlls=no /S"

DetailPrint "Uninstalling GNU Scientific Library (GSL)"
!insertmacro UnInstallLib REGDLL NOTSHARED REBOOT_PROTECTED "$INSTDIR\Lib\libgsl-0.dll"
!insertmacro UnInstallLib REGDLL NOTSHARED REBOOT_PROTECTED "$INSTDIR\Lib\libgslcblas-0.dll"

DetailPrint "Uninstalling GNU Scientific Library Fortran bindings (FGSL)"
!insertmacro UnInstallLib REGDLL NOTSHARED REBOOT_PROTECTED "$INSTDIR\Lib\libfgsl-0.dll"

DetailPrint "Uninstalling HDF5 (with Fortran bindings)"
!insertmacro UnInstallLib REGDLL NOTSHARED REBOOT_PROTECTED "$INSTDIR\Lib\libhdf5-6.dll"
!insertmacro UnInstallLib REGDLL NOTSHARED REBOOT_PROTECTED "$INSTDIR\Lib\libhdf5_fortran-6.dll"

DetailPrint "Uninstalling libxml2"
!insertmacro UnInstallLib REGDLL NOTSHARED REBOOT_PROTECTED "$INSTDIR\Lib\libxml2-2.dll" 

DetailPrint "Uninstalling libxslt"
!insertmacro UnInstallLib REGDLL NOTSHARED REBOOT_PROTECTED "$INSTDIR\Lib\libxslt-1.dll" 

DetailPrint "Uninstalling xraylib (with Fortran bindings)"
!insertmacro UnInstallLib REGDLL NOTSHARED REBOOT_PROTECTED "$INSTDIR\Lib\libxrl-3.dll" 
!insertmacro UnInstallLib REGDLL NOTSHARED REBOOT_PROTECTED "$INSTDIR\Lib\libxrlf03-3.dll" 

DetailPrint "Uninstalling gfortran runtime libraries"
!insertmacro UnInstallLib REGDLL NOTSHARED REBOOT_PROTECTED "$INSTDIR\Lib\libgfortran-3.dll" 

DetailPrint "Uninstalling OpenMP runtime libraries"
!insertmacro UnInstallLib REGDLL NOTSHARED REBOOT_PROTECTED "$INSTDIR\Lib\libgomp-1.dll" 

DetailPrint "Uninstalling POSIX threads runtime libraries"
!insertmacro UnInstallLib REGDLL NOTSHARED REBOOT_PROTECTED "$INSTDIR\Lib\pthreadGC2.dll" 

DetailPrint "Uninstalling XMIMSIM runtime library"
!insertmacro UnInstallLib REGDLL NOTSHARED REBOOT_PROTECTED "$INSTDIR\Lib\libxmimsim-0.dll" 

RMDir "$INSTDIR\Lib"

DetailPrint "Uninstalling XMIMSIM executables"
Delete "$INSTDIR\Bin\xmimsim.exe"
Delete "$INSTDIR\Bin\xmimsim-db.exe"
Delete "$INSTDIR\Bin\xmimsim-gui.exe"
Delete "$INSTDIR\Bin\xmimsim-pymca.exe"
Delete "$INSTDIR\Bin\xmimsim-xmso2xmsi.exe"

RMDir "$INSTDIR\Bin"

DetailPrint "Uninstalling auxiliary files"
Delete "$INSTDIR\Share\xmimsimdata.h5"
Delete "$INSTDIR\Share\xmso2xmsi.xml"
Delete "$INSTDIR\Share\xmimsim_cat.xml"
Delete "$APPDATA\xmimsim\xmimsim-solid-angles.h5"

RMDir "$APPDATA\xmimsim"
RMDir "$INSTDIR\Share"



Delete "$INSTDIR\Uninstall.exe"
RMDir "$INSTDIR"

${un.EnvVarUpdate} $0 "PATH" "R" "HKLM" "$INSTDIR\Bin"
;clean registry
DeleteRegKey HKLM "Software\${PRODUCT_NAME}\data"
DeleteRegKey HKLM "Software\${PRODUCT_NAME}\catalog"
DeleteRegKey HKLM "Software\${PRODUCT_NAME}"
DeleteRegKey HKLM "${PRODUCT_UNINST_KEY}"


SectionEnd

Function .onInit
ReadRegStr $R0 HKLM ${PRODUCT_UNINST_KEY} "UninstallString"
StrCmp $R0 "" done
MessageBox MB_OKCANCEL|MB_ICONEXCLAMATION "${PRODUCT_NAME} is already installed. $\n$\nClick `OK` to remove the previous version or `Cancel` to cancel this upgrade." IDOK uninst
Abort

uninst:
ClearErrors
Exec $INSTDIR\Uninstall.exe

done:

FunctionEnd

